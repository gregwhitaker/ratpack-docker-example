import com.bmuschko.gradle.docker.tasks.image.*

plugins {
    id 'application'
    id 'io.ratpack.ratpack-java' version '1.5.0'
    id "com.bmuschko.docker-remote-api" version "3.2.1"
}

mainClassName = 'com.github.gregwhitaker.ratpackdocker.example.Main'
sourceCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    compile ratpack.dependency("guice")
    compile 'com.github.gregwhitaker:ratpack-error:1.5.0'

    runtime 'org.slf4j:slf4j-simple:1.7.22'

    testCompile 'junit:junit:4.12'
}

distZip {
    // You only need to set this if you have specified an archive version. This removes the version number
    // from the final distribution package which makes it easier to script in your Dockerfile.
    archiveName = 'ratpack-docker-example.zip'
}

task buildImage(type: DockerBuildImage) {
    inputDir = new File("${projectDir}")
    dockerFile = new File("${projectDir}/src/docker/Dockerfile")
}